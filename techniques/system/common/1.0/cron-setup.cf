# SPDX-License-Identifier: GPL-3.0-or-later
# SPDX-FileCopyrightText: 2021 Normation SAS

bundle agent setup_cronjob
{

  vars:
    solaris::
      "crontab_group" string => "root";
    aix::
      "crontab_group" string => "cron";

  files:

    !aix.!solaris::
      "/etc/cron.d/rudder-agent"
        create          => "true",
        perms           => mog("644", "root", "0"),
        edit_defaults   => empty_backup,
        template_method => "mustache",
        edit_template   => "${sys.workdir}/inputs/common/cron/rudder-agent-community-cron";

    aix|solaris::
      # Cleanup the crontab
      "/var/spool/cron/crontabs/root"
        edit_defaults => noempty_backup,
        edit_line => delete_lines_matching("0,5,10,15,20,25,30,35,40,45,50,55 \* \* \* \* if \[ -x /opt/rudder/bin/check-rudder-agent \]; then /opt/rudder/bin/check-rudder-agent( >/dev/null)?; fi");

      # Cleanup the pre-4.0 crontab
      "/var/spool/cron/crontabs/root"
        edit_defaults => noempty_backup,
        edit_line => delete_lines_matching("0,5,10,15,20,25,30,35,40,45,50,55 \* \* \* \* /usr/bin/test -x /opt/rudder/bin/check-rudder-agent.*");

      # Cleanup the crontab without logfile
      "/var/spool/cron/crontabs/root"
        edit_defaults => noempty_backup,
        edit_line => delete_lines_matching("0,5,10,15,20,25,30,35,40,45,50,55 \* \* \* \* /usr/bin/test -x /opt/rudder/bin/rudder && /opt/rudder/bin/rudder agent check >/dev/null");

      # Cleanup crontab again
      "/var/spool/cron/crontabs/root"
        edit_defaults => noempty_backup,
        edit_line => delete_lines_matching("0,5,10,15,20,25,30,35,40,45,50,55 \* \* \* \* /usr/bin/test -x /opt/rudder/bin/rudder && /opt/rudder/bin/rudder agent check >> /var/log/rudder/agent-check/check.log 2>&1");

      # Add Rudder entry
      "/var/spool/cron/crontabs/root"
        create        => "true",
        perms         => mog("600", "root", "${crontab_group}"),
        edit_line     => insert_lines("0,5,10,15,20,25,30,35,40,45,50,55 * * * * /opt/rudder/bin/rudder agent check -q >> /var/log/rudder/agent-check/check.log 2>&1"),
        classes       => classes_generic("rudder_system_user_crontab_insertion"),
        comment       => "Insert an AIX/Solaris-compatible user crontab to run /opt/rudder/bin/rudder agent check";



}

#######################################################
# Check if the cron daemon is running
# This only works with unix flavoured systems too

bundle agent check_cron_daemon
{
  vars:

    redhat|fedora|slackware::
      "service_name" string => "crond";
    !(redhat|fedora|slackware)::
      "service_name" string => "cron";

  classes:

    any::
      "pass3" expression => "pass2";
      "pass2" expression => "pass1";
      "pass1" expression => "any";

  methods:
      "restart_cron" usebundle => disable_reporting;
      "restart_cron" usebundle => _method_reporting_context("CRON Daemon", "None");
      "check_cron_service"
                     usebundle => service_check_running("${service_name}"),
                            if => "!aix";

      "restart_cron" usebundle => service_restart("${service_name}"),
                            if => "(rudder_system_user_crontab_insertion_repaired|service_check_running_${service_name}_error).!aix";
      "restart_cron" usebundle => enable_reporting;


    pass3::
      "any" usebundle => rudder_common_reports_generic("Common", "service_restart_${service_name}", "${system_common.directiveId}", "CRON Daemon", "None", "Cron daemon status"),
                   if => "(rudder_system_user_crontab_insertion_repaired|service_check_running_${service_name}_error).!aix";

      "any" usebundle => rudder_common_report("Common", "result_success", "${system_common.directiveId}", "CRON Daemon", "None", "CRON is correctly running"),
                   if => "!(rudder_system_user_crontab_insertion_repaired|service_check_running_${service_name}_error).!aix";
    aix::
      "any" usebundle => rudder_common_report("Common", "result_na", "${system_common.directiveId}", "CRON Daemon", "None", "CRON is not a service on AIX: CRON verifications skipped");



}
